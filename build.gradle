ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

buildscript {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    repositories {
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
    }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
//apply from: file('gradle/check.gradle')
//apply from: file('gradle/license.gradle') // Waiting for re-release
apply from: file('gradle/release.gradle')

apply plugin: 'idea'
apply plugin: 'eclipse'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'

    group = "com.netflix.${githubProjectName}" // TEMPLATE: Set to organization of project

    dependencies {
        testCompile 'org.testng:testng:6.8.1'
    }
}

project(':exhibitor-core') {
    dependencies {
        compile('org.apache.zookeeper:zookeeper:3.4.6') {
            exclude group: 'com.sun.jmx', module: 'jmxri'
            exclude group: 'com.sun.jdmk', module: 'jmxtools'
            exclude group: 'javax.jms', module: 'jms'

            // JLine pulls this in as a compile dependency, they have fixed it in future versions
            exclude group: 'junit', module: 'junit'
        }
        // compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
        compile 'com.amazonaws:aws-java-sdk:1.9.10'
        compile 'com.google.guava:guava:14.0.1'
        compile 'com.netflix.servo:servo-core:0.7.7'
        compile 'com.sun.jersey:jersey-bundle:1.18'
        compile 'com.sun.jersey:jersey-client:1.18'
        compile 'javax.ws.rs:jsr311-api:1.1.1'
        compile 'javax.xml.ws:jaxws-api:2.2.11'
        compile 'org.apache.curator:curator-framework:2.3.0'
        compile 'org.apache.curator:curator-recipes:2.3.0'
        compile 'org.apache.lucene:lucene-core:3.6.2'
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
        compile 'org.codehaus.jsr166-mirror:jsr166y:1.7.0'

        testCompile 'org.apache.curator:curator-test:2.7.0'
        testCompile 'org.mortbay.jetty:jetty:6.1.26'
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    jar {
        manifest {
            attributes (
                'Implementation-Title': 'exhibitor-core',
                'Implementation-Version': project.version
            )
        }
    }
}

project(':exhibitor-standalone') {
    dependencies {
        compile (project(":exhibitor-core")) {
            exclude group: 'com.sun.jersey', module: 'jersey-client'
        }
        compile 'com.amazonaws:aws-java-sdk:1.9.10'
        compile 'com.google.guava:guava:14.0.1'
        compile 'com.sun.jersey:jersey-bundle:1.18'
        // compile 'com.sun.jersey:jersey-client:1.18'
        compile 'commons-cli:commons-cli:1.2'
        compile 'javax.servlet:servlet-api:2.5'
        compile 'javax.xml.ws:jaxws-api:2.2.11'
        // compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
        compile 'org.mortbay.jetty:jetty:6.1.26'
        compile 'org.slf4j:slf4j-log4j12:1.7.7'
    }
    jar {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        manifest {
            attributes (
            'Main-Class': 'com.netflix.exhibitor.application.ExhibitorMain',
            'Implementation-Title': 'exhibitor-standalone',
            'Implementation-Version': project.version
            )
        }
    }
}
